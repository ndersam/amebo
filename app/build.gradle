apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: "kotlin-allopen"
apply plugin: 'pandora-plugin'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

apply from: '../shared_dependencies.gradle'

def keyStoreProps = new Properties()
def releasePropsFilename = 'release.properties'

android {
    compileSdkVersion 30

    signingConfigs {
        release {
            if (project.rootProject.file(releasePropsFilename).exists()) {
                keyStoreProps.load(project.rootProject.file(releasePropsFilename).newDataInputStream())
                storeFile file(keyStoreProps['storeFile'])
                storePassword keyStoreProps['storePassword']
                keyAlias keyStoreProps['keyAlias']
                keyPassword keyStoreProps['keyPassword']
            }
        }
    }

    defaultConfig {
        multiDexEnabled true
        applicationId "com.amebo.amebo"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 23
        versionName "1.2.0"

        testInstrumentationRunner "com.amebo.amebo.suite.TestRunner"
    }

    // Robolectric resource processing/loading
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    buildTypes {
        debug {
            splits.abi.enable = false
            splits.density.enable = false
            aaptOptions.cruncherEnabled = false
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            debuggable false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding = true
        dataBinding = false
    }

    sourceSets {
        androidTest {
            java.srcDirs += "src/sharedTest/java"
            kotlin.srcDirs += "src/sharedTest/java"
        }
        test {
            java.srcDirs += "src/sharedTest/java"
            kotlin.srcDirs += "src/sharedTest/java"
        }
    }
    ndkVersion '21.3.6528147'

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
        exclude("META-INF/*.kotlin_module")
    }

    configurations.all {
        resolutionStrategy {
            exclude group: "org.jetbrains.kotlinx", module: "kotlinx-coroutines-debug"
        }
    }

}


dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0'
    implementation 'androidx.browser:browser:1.2.0'
    implementation project(path: ':core')

    implementation 'com.google.android.material:material:1.2.1'
    implementation 'com.ncapdevi:frag-nav:3.2.0'

    // Utils
    implementation 'com.airbnb.android:lottie:3.3.0'
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    kapt 'com.github.bumptech.glide:compiler:4.11.0'


    // Jetpack
    implementation "android.arch.lifecycle:extensions:1.1.1"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.2.0"
    implementation "androidx.preference:preference-ktx:1.1.1"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.2.0"
    implementation "androidx.lifecycle:lifecycle-common-java8:2.2.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0"


    def fragment_version = "1.3.0-alpha04"
    implementation "androidx.fragment:fragment-ktx:$fragment_version"

    def work_version = "2.3.4"
    implementation "androidx.work:work-runtime-ktx:$work_version"
    testImplementation "androidx.work:work-testing:$work_version"
    androidTestImplementation "androidx.work:work-testing:$work_version"


    // Views
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta6'
    implementation "ru.tinkoff.scrollingpagerindicator:scrollingpagerindicator:1.0.6"
    implementation 'com.github.chrisbanes:PhotoView:2.1.3'
    implementation 'pl.droidsonroids.gif:android-gif-drawable:1.2.19'
    implementation 'com.pierfrancescosoffritti.androidyoutubeplayer:core:10.0.5'
    implementation 'de.hdodenhof:circleimageview:3.0.0'
    implementation 'com.mikepenz:materialdrawer:8.0.0'
    implementation 'com.github.phcannessondkt:flexbox-layout:2.0.2-ph' // Fixes this issue https://github.com/google/flexbox-layout/issues/420
    implementation 'androidx.recyclerview:recyclerview:1.2.0-alpha06'
    implementation 'com.github.RobertApikyan:SegmentedControl:1.2.0'
    implementation 'com.github.HeyAlex:BottomDrawer:v1.0.0'
    implementation 'com.github.rtugeek:colorseekbar:1.7.5'
    implementation 'com.getkeepsafe.taptargetview:taptargetview:1.13.0'

    // Testing
    debugImplementation "androidx.fragment:fragment-testing:$fragment_version"


    def multidex_version = "2.0.1"
    implementation "androidx.multidex:multidex:$multidex_version"


    // Espresso
    //===============================
    def espresso_core_version = '3.2.0'
    def espresso_intents_version = '3.2.0'
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_core_version"
    androidTestImplementation "androidx.test.espresso:espresso-intents:$espresso_intents_version"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$espresso_core_version"
    testImplementation "androidx.test.espresso:espresso-core:$espresso_core_version"
    testImplementation "androidx.test.espresso:espresso-intents:$espresso_intents_version"
    testImplementation "androidx.test.espresso:espresso-contrib:$espresso_core_version"


    // Debugging
    debugImplementation 'com.facebook.stetho:stetho:1.5.1'
    debugImplementation 'com.facebook.stetho:stetho-okhttp3:1.5.1'
    debugImplementation "com.github.whataa:pandora:androidx_v2.1.0"
    releaseImplementation "com.github.whataa:pandora-no-op:v2.0.3"
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.3'

    // Play
    implementation 'com.google.android.play:core-ktx:1.8.1'
}
